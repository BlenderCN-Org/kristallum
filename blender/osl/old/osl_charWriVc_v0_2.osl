// https://github.com/imageworks/OpenShadingLanguage/blob/master/src/doc/osl-languagespec.pdf
// https://docs.blender.org/manual/en/dev/render/cycles/nodes/osl.html

#define WRI_OBJECT_PREFIX "wri_"
#define WRI_VERTEXCOL_NAME "Wri"
#define WRI_GRIDT_MINANG 0.1
#define MAXCACHE 100
#define MAXCACHEx3 300
#define FALLOFF_POW 1

#define ISWHITE(cl) (fabs(cl[0]-cl[1])<0.01 && fabs(cl[0]-cl[2])<0.01)

shader charWriVc (
	point base_Frac_Steps = point(0.004, 0, 3),
	point wri_DirectFalloff = point(20,130,300),
	point wri_SideFalloff = point(20,130,300),
	point wri_WhiteFalloff = point(10,10,10),
	point wri_Levels = point(1,1,1),
	float wri_NormalBlend = 0.1,

	point in_pos = point(0,0,0),
	point in_nrm = point(0,0,0),
	color in_color = color(0,0,0),
	string in_color_vcname = "",
	string wri_ObjPrefix = "",

	output float out_Total = 0.0,
	output point out_Normal = 0.0,
	output float WriR = 0.0,
	output float WriG = 0.0,
	output float WriB = 0.0
){
	point rbtPixels = transform("raster", point("NDC", 1,1,0));
	float pixelWidth = min(rbtPixels[0],rbtPixels[1])*base_Frac_Steps[0];

	string vcname = in_color_vcname;
	if(strlen(vcname)<3){
		vcname = WRI_VERTEXCOL_NAME;
	}

	point pThis = length(in_pos)>0?in_pos:P;
	point nThis = length(in_nrm)>0?in_nrm:N;
	point pCamWrl = point("camera", 0, 0, 0);
	point pThisRST = transform("raster", pThis);

	if(wri_NormalBlend>0){
		string WOP = wri_ObjPrefix;
		if(strlen(WOP)<3){
			WOP = WRI_OBJECT_PREFIX;
		}
		string thisId;
		getattribute("geom:name",thisId);
		if(startswith(thisId,WOP) > 0){
			string hitId;
			point pCurr = pThis;
			point pSideHit;
			point nSideHit;
			int isHit = trace(pCurr-nThis*0.00001, -nThis);
			getmessage("trace","geom:name",hitId);
			getmessage("trace","P",pSideHit);
			getmessage("trace","N",nSideHit);
			if(isHit > 0 && startswith(hitId,WOP) < 1){
				float wriDist = length(pCurr-pSideHit);
				wriDist = wriDist/wri_NormalBlend;
				nThis = mix(nSideHit,nThis,clamp(wriDist,0,1));
				//nThis = nSideHit;
			}
		}
	}

	int ptncIdi[3] = {0, MAXCACHE, MAXCACHE*2};
	int ptncIdx[3] = {0, MAXCACHE, MAXCACHE*2};
	point potencialsPoints[MAXCACHEx3] = {0};
	point potencialsPoin3d[MAXCACHEx3] = {0};
	float potencialsLevels[MAXCACHEx3] = {0};
	if(fabs(wri_Levels[0])+fabs(wri_Levels[1])+fabs(wri_Levels[2])>0.001){
		float testStep = 1;
		if(base_Frac_Steps[2]>0){
			testStep = base_Frac_Steps[2]*1/(2*pixelWidth);
		}else{
			testStep = max(0.01,1/base_Frac_Steps[1]);
		}
		for(float ii=0; ii<1; ii = ii+testStep){
			float xi=mix(-pixelWidth,pixelWidth, ii);
			for(float jj=0; jj<1; jj = jj+testStep){
				float yi=mix(-pixelWidth,pixelWidth, jj);
				point pOffs = point(xi,yi,0);
				point pSideRst = pThisRST+pOffs;
				point pSideWlr = transform("raster", "world", pSideRst);
				int isHit = trace(pCamWrl, pSideWlr-pCamWrl);
				if(isHit > 0){
					point hitPos = point(0,0,0);
					color hitColor = color(0,0,0);
					getmessage("trace", vcname, hitColor);
					getmessage("trace", "P", hitPos);
					for(int chnl=0;chnl<3;chnl++){
						float hitLevel = hitColor[chnl];
						if(hitLevel > 0.00001){
							if(ISWHITE(hitColor)){
								// Stopper in this direction
								hitLevel = -1*hitLevel;
							}
							float angl = atan2(pOffs[1],pOffs[0]);
							int iReplacedOld = 0;
							for(int kk=ptncIdi[chnl];kk<ptncIdx[chnl];kk++){
								float angl2 = atan2(potencialsPoints[kk][1],potencialsPoints[kk][0]);
								//  || fabs(angl2+M_2PI-angl) < WRI_GRIDT_MINANG
								if((fabs(angl2-angl) < WRI_GRIDT_MINANG) && length(pOffs)<length(potencialsPoints[kk])){
									iReplacedOld = iReplacedOld+1;
									potencialsPoints[kk] = pOffs;
									potencialsLevels[kk] = hitLevel;
									potencialsPoin3d[kk] = hitPos-pThis;
								}
							}
							if(iReplacedOld == 0 && ptncIdx[chnl] < ptncIdi[chnl]+MAXCACHE-1){
								potencialsPoints[ptncIdx[chnl]] = pOffs;
								potencialsLevels[ptncIdx[chnl]] = hitLevel;
								potencialsPoin3d[ptncIdx[chnl]] = hitPos-pThis;
								ptncIdx[chnl] = ptncIdx[chnl]+1;
							}
						}
					}
				}
			}
		}
	}
	for(int chnl=0;chnl<3;chnl++){
		float Wri = 0;
		float antiWri = 0;
		float antiWriCc = 0;
		float dirfo = wri_DirectFalloff[chnl];//3d
		float sidfo = wri_SideFalloff[chnl]*0.01;//2d pixels
		float whifo = wri_WhiteFalloff[chnl];//2d pixels
		for(int i=ptncIdi[chnl];i<ptncIdx[chnl];i++){
			if(potencialsLevels[i]>0)
			{
				for(int j=i+1;j<ptncIdx[chnl];j++){
					if(potencialsLevels[j]>0)
					{
						float raylen = length(potencialsPoints[j]-potencialsPoints[i]);
						float pIlen = length(potencialsPoints[i]);
						float pJlen = length(potencialsPoints[j]);
						float otklon = fabs((pIlen+pJlen)-raylen);
						if(raylen < pixelWidth*1.3){
							// Lengthy lines JUMPS when window moves
							//float raylen3d = length(potencialsPoin3d[j]-potencialsPoin3d[i]);
							//float pIlen3d = length(potencialsPoin3d[i]);
							//float pJlen3d = length(potencialsPoin3d[j]);
							//float otklon3d = fabs((pIlen3d+pJlen3d)-raylen3d);
							float level = max(potencialsLevels[i]*(1.0-pIlen/raylen), potencialsLevels[j]*(1.0-pJlen/raylen));
							level = clamp(level-pow(sidfo*otklon,FALLOFF_POW),0.0,1.0);
							if(level>0){
								Wri = max(Wri,level);
							}
						}
					}
				}
				if(dirfo>0){
					float pIlen3d = length(potencialsPoin3d[i]);
					float level = clamp(potencialsLevels[i]-pow(dirfo*pIlen3d,FALLOFF_POW),0.0,1.0);
					if(level>0){
						Wri = max(Wri,level);
					}
				}
			}else{
				float pIlen = length(potencialsPoints[i]);
				antiWri = antiWri+clamp(1.0-pIlen/whifo,0,1)*fabs(potencialsLevels[i]);
				antiWriCc = antiWriCc+1;
			}
		}
		Wri = Wri - antiWri/antiWriCc;
		if(chnl == 0){
			WriR = Wri;
		}else if(chnl == 1){
			WriG = Wri;
		}else if(chnl == 2){
			WriB = Wri;
		}
	}
	out_Total = max(max(wri_Levels[0]*WriR,wri_Levels[1]*WriG),wri_Levels[2]*WriB);
	out_Normal = nThis;
}
