// https://github.com/imageworks/OpenShadingLanguage/blob/master/src/doc/osl-languagespec.pdf
// https://docs.blender.org/manual/en/dev/render/cycles/nodes/osl.html
// To prevent "AO leaking" ignoring far points (near plane is not affected by objects BEHIND it)
// Wrinkles-objects should have special prefix
#define PUMASKIN_PREFIX "wri_"
#define PUMAUVMAP_NAME "UVMap"
//#define PUMAVCMAP_NAME "Col"

#define NORMALIZE(value, nrm, normalAffect, clampArray) {\
    float res = value;\
    if(normalAffect>0.0001){\
        res = res*mix(1,nrm,normalAffect);\
    }else if(normalAffect<0.0001){\
        res = res*mix(1,(1-nrm),-1*normalAffect);\
    }\
    if(fabs(clampArray[1] - clampArray[0]) > 0.0001){res = (res - clampArray[0])/(clampArray[1]-clampArray[0]);res = clamp(res,0,1);};\
    res = pow(res,clampArray[2]);\
    value = res;\
}


shader charMat (
    float baseLevel = 0.5,
    point pixel_Wfrac_Steps = point(0.02, 10, 0),

    float lt_Level = 0.5,
    point lt_Dir = point(0, -1.1, 1),
    point lt_Size_Pow_Nafk = point(0.5, 1.0, 2.0),
    point lt_Normalize = point(0.0, 0.4, 1.0),

    float do_Level = -0.1,
    point do_Dir = point(0, -1.1, 0.3),
    point do_Size_Pow_Nafk = point(0.1, 1.0, 0.0),
    point do_Normalize = point(0.0, 1.0, 1.0),

    float ao_Level = -0.3,
    point ao_Size_Dist_Vafk = point(1.0, 0.1, 1.0),
    point ao_Normalize = point(0.3, 0.7, 1.0),

    float sp_Level = 0.2,
    point sp_Size_Dist_Vafk = point(0.5, -0.1, 0.0),
    point sp_Normalize = point(0.0, 1.0, 1.0),

    float wri_Level = 0.0,
	point wri_UVMap_fake = point(0,0,0),
    point wri_Size_Offs_Area = point(1.0, 0.03, 0.01),
    point wri_Normalize = point(0.0, 0.3, 1.0),

    output float Total = 0.0,
    output float Lt = 0.0,
    output float Do = 0.0,
    output float Ao = 0.0,
    output float Sp = 0.0,
    output float WriDst = 0.0,
	//output color WriCol = color(0,0,0),
    output point WriGrd = point(0,0,0)
){

    point rbtPixels = transform("raster", point("NDC", 1,1,0));
    float pixelWidth = min(rbtPixels[0],rbtPixels[1])*pixel_Wfrac_Steps[0];
    //float pixelStep = pixelWidth/max(0.01,pixel_Wfrac_Steps[1]);

    float ltSoftness = lt_Size_Pow_Nafk[0];
    float ltExpL = lt_Size_Pow_Nafk[1];
    float ltNormalAffect = lt_Size_Pow_Nafk[2];

    float aoSoftness = ao_Size_Dist_Vafk[0];
    float aoMinDst = ao_Size_Dist_Vafk[1];
    float aoViewAffect = ao_Size_Dist_Vafk[2];

    float doSoftness = do_Size_Pow_Nafk[0];
    float doExpL = do_Size_Pow_Nafk[1];
    float doNormalAffect = do_Size_Pow_Nafk[2];

    float spSoftness = sp_Size_Dist_Vafk[0];
    float spMinDst = sp_Size_Dist_Vafk[1];
    float spViewAffect = sp_Size_Dist_Vafk[2];

    float wriSoftness = wri_Size_Offs_Area[0];
	float wriGradientOffset = wri_Size_Offs_Area[1];
	float wriGradientArea = wri_Size_Offs_Area[2];
    float wriMax = wri_Normalize[1];

    aoSoftness = max(0.0,aoSoftness)*(fabs(ao_Level)<0.0001?0:1);
    doSoftness = max(0.0,doSoftness)*(fabs(do_Level)<0.0001?0:1);
    spSoftness = max(0.0,spSoftness)*(fabs(sp_Level)<0.0001?0:1);
    ltSoftness = max(0.0,ltSoftness)*(fabs(lt_Level)<0.0001?0:1);
    wriSoftness = max(0.0,wriSoftness)*(fabs(wri_Level)<0.0001?0:1);

    point pThis = P;
    point nThis = N;
    string thisId;
    getattribute("geom:name",thisId);
    point pCamWrl = point("camera", 0, 0, 0);
    point pThisRST = transform("raster", pThis);
    float distBase = length(pCamWrl-pThis);
    point nFarFarAway = pCamWrl+9999*(pCamWrl-pThis);

    point pSideHit;
    point nSideHit;
    float hitDist = 0;
    float hitCamDist = 0;
    float wriDist = 0;
	point wriNrm = nThis;
    point wriPos = pThis;
	point wriTexUV;
	//point wriCol;
    int isHit = 0;
    string hitId;
    if(startswith(thisId,PUMASKIN_PREFIX) > 0){
		getattribute(PUMAUVMAP_NAME,wriTexUV);
        //getattribute(PUMAVCMAP_NAME,wriCol);
        getattribute("N",wriNrm);
        wriPos = pThis;
        point pCurr = pThis;
        for(int skipcc = 0;skipcc<5;skipcc++){
            isHit = trace(pCurr-nThis*0.00001, -nThis);
            getmessage("trace","geom:name",hitId);
            getmessage("trace","P",pSideHit);
			getmessage("trace","N",nSideHit);
            if(isHit > 0 && startswith(hitId,PUMASKIN_PREFIX) > 0){
                // ignoring wri intersections!
                wriDist = wriDist+length(pCurr-pSideHit);
                pCurr = pSideHit;
            }else{
                break;
            }
        }
        if(isHit > 0 && startswith(hitId,PUMASKIN_PREFIX) < 1){
            wriDist = wriDist+length(pCurr-pSideHit);
			nThis = mix(nSideHit,nThis,clamp(wriDist/wriMax,0,1));
        }else{
            wriDist = 0;
        }
    }else
    {
        point pCurr = pThis;
		point pCurrN = -nThis;//-I;
        for(int skipcc = 0;skipcc<5;skipcc++){
            isHit = trace(pCurr+pCurrN*0.00001, pCurrN);
            getmessage("trace","geom:name",hitId);
            getmessage("trace","P",pSideHit);
            getmessage("trace","N",wriNrm);
			getmessage("trace",PUMAUVMAP_NAME,wriTexUV);
			//getmessage("trace",PUMAVCMAP_NAME,wriCol);
            if(isHit > 0 && startswith(hitId,PUMASKIN_PREFIX) < 1){
                // ignoring non-wri intersections!
                wriDist = wriDist+length(pCurr-pSideHit);
                pCurr = pSideHit;
            }else{
                break;
            }
        }
        if(isHit > 0 && startswith(hitId,PUMASKIN_PREFIX) > 0){
            wriDist = wriDist+length(pCurr-pSideHit);
            wriPos = pSideHit;
        }else{
            wriDist = 0;
        }
    }
    if(wriDist >= wriMax){
        wriDist = 0;
    }
    if(wriDist < 0.0001){
		wriSoftness = 0.0;
    }
    point ltDirN = normalize(lt_Dir);
    float ltDot = max(0,dot(nThis,ltDirN));
    point doDirN = normalize(do_Dir);
    float doDot = max(0,dot(nThis,doDirN));
    point viDirN = normalize(pCamWrl-pThis);
    float viDot = max(0,dot(nThis,viDirN));

    float aoLevlCount = 0;
    float spLevlCount = 0;
    float ltLevlCount = 0;
    float doLevlCount = 0;

    float testTotalAo = 0;
    float testTotalSp = 0;
    float testTotalLt = 0;
    float testTotalDo = 0;
	point wriMediatorDir = point(0,0,0);
	float wriMediatorMin = 9999;
    if(pixelWidth > 0 && fabs(ao_Level)+fabs(sp_Level)
        +fabs(lt_Level)+fabs(do_Level)
		+fabs(wriSoftness)>0){
		float testWidth = pixelWidth;
		float testStep = 1;
		if(pixel_Wfrac_Steps[2]>0){
			testStep = 1/(2*testWidth)*pixel_Wfrac_Steps[2];
		}else{
			testStep = max(0.01,1/pixel_Wfrac_Steps[1]);
		}
        for(float ii=0; ii<1; ii = ii+testStep){
			float xi=mix(-testWidth,testWidth, ii);
            for(float jj=0; jj<1; jj = jj+testStep){
				float yi=mix(-testWidth,testWidth, jj);
                float ptlen = sqrt(xi*xi+yi*yi);
                if(ptlen > testWidth+0.1){
                    continue;
                }
                float ltWeight = (1.0-ptlen/(testWidth*ltSoftness))*(ltSoftness>0?1:0);
                float doWeight = (1.0-ptlen/(testWidth*doSoftness))*(doSoftness>0?1:0);
                float aoWeight = (1.0-ptlen/(testWidth*aoSoftness))*(aoSoftness>0?1:0);
                float spWeight = (1.0-ptlen/(testWidth*spSoftness))*(spSoftness>0?1:0);
                float wriWeight = (1.0-ptlen/(testWidth*wriSoftness))*(wriSoftness>0?1:0);
                if(ltWeight <= 0 && doWeight <= 0
                    && aoWeight <= 0 && spWeight <= 0
					&& wriWeight <= 0){
                    continue;
                }

                point pSideRst = pThisRST+point(xi,yi,0);
                point pSideWlr = transform("raster", "world", pSideRst);
                isHit = trace(pCamWrl, pSideWlr-pCamWrl);
                if(isHit > 0){
                    getmessage("trace","N",nSideHit);
                    getmessage("trace","P",pSideHit);
                    getmessage("trace","geom:name",hitId);
                }else{
                    nSideHit = nThis;
                    pSideHit = nFarFarAway;
                }
                point dir2hit = pThis-pSideHit;
                hitDist = length(dir2hit);
                hitCamDist = length(pSideHit-pCamWrl);

                if(ltWeight > 0){
                    testTotalLt = testTotalLt+1;
                    int isHit2 = trace(pSideHit+ltDirN*0.001, ltDirN);
                    if(isHit2 < 1){
                        ltLevlCount = ltLevlCount+1*ltWeight;
                    }
                }
                if(doWeight > 0){
                    testTotalDo = testTotalDo+1;
                    int isHit2 = trace(pSideHit+doDirN*0.001, doDirN);
                    if(isHit2 > 0){
                        doLevlCount = doLevlCount+1*doWeight;
                    }
                }

                if(aoWeight > 0){
                    testTotalAo = testTotalAo+1;
                    float aoHitDist = hitDist;
                    if(aoMinDst == 0.0){
                        aoLevlCount = aoLevlCount + aoWeight*aoHitDist;
                    } else if((hitCamDist <= distBase && aoMinDst>0)
                        || (hitCamDist >= distBase && aoMinDst<0))
                    {
                        if(aoWeight*aoHitDist > fabs(aoMinDst)){
                            aoLevlCount = aoLevlCount+1;
                        }else{
                            aoLevlCount = aoLevlCount+aoWeight*aoHitDist/fabs(aoMinDst);
                        }
                    }
                }

                if(spWeight > 0){
                    testTotalSp = testTotalSp+1;
                    float dist2pln = fabs(dot(dir2hit,nThis));
                    if(spMinDst == 0.0){
                        spLevlCount = spLevlCount + spWeight*dist2pln;
                    } else if((hitCamDist <= distBase && spMinDst>0)
                        || (hitCamDist >= distBase && spMinDst<0))
                    {
                        if(spWeight*dist2pln > fabs(spMinDst)){
                            spLevlCount = spLevlCount+1;
                        }else{
                            spLevlCount = spLevlCount+spWeight*dist2pln/fabs(spMinDst);
                        }
                    }
                }

                if(wriWeight > 0 && isHit > 0){
                    int isHit2 = 0;
					string hitId2 = hitId;
                    point pCurr = pSideHit;
					point pCurr2 = pCurr;
					point pCurr2UV = wriTexUV;
					point pCurrDir = nSideHit;
                    for(int skipcc = 0;skipcc<5;skipcc++){
                        isHit2 = trace(pCurr-pCurrDir*0.00001, -pCurrDir);
                        getmessage("trace","P",pCurr2);
						getmessage("trace","geom:name",hitId2);
						getmessage("trace",PUMAUVMAP_NAME,pCurr2UV);
                        if(isHit2 > 0 && startswith(hitId2,PUMASKIN_PREFIX) < 1){
                            pCurr = pCurr2;
                        }else{
                            break;
                        }
                    }
                    if(isHit2 > 0 && startswith(hitId2,PUMASKIN_PREFIX) > 0){
						float uvdif = length(pCurr2UV-wriTexUV);
						float uvdifV = fabs(uvdif - wriGradientOffset);
						if(pCurr2UV[1]>wriTexUV[1] && uvdifV < wriMediatorMin)
						{
							wriMediatorMin = uvdifV;
							wriMediatorDir = pSideHit;
						}
                    }
                }
            }
        }
    }

    Lt = pow(ltLevlCount/testTotalLt,ltExpL);
    NORMALIZE(Lt,ltDot,ltNormalAffect,lt_Normalize);

    Do = pow(doLevlCount/testTotalDo,doExpL);
    NORMALIZE(Do,doDot,doNormalAffect,do_Normalize);

    Ao = aoLevlCount/testTotalAo;
    NORMALIZE(Ao,viDot,aoViewAffect,ao_Normalize);

    Sp = spLevlCount/testTotalSp;
    NORMALIZE(Sp,viDot,spViewAffect,sp_Normalize);

    //WriCol = wriCol;
    WriDst = wriDist;
    NORMALIZE(WriDst,viDot,0,wri_Normalize);
	point pThisRST2 = transform("raster", wriMediatorDir);
	WriGrd = (wriDist>0)?(pThisRST2-pThisRST):0;

    Total = baseLevel
        +Ao*ao_Level
        +Sp*sp_Level
        +Lt*lt_Level
        +Do*do_Level
        +WriDst*wri_Level;
} 