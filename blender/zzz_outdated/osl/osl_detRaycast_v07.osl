// https://github.com/imageworks/OpenShadingLanguage/blob/master/src/doc/osl-languagespec.pdf
// https://docs.blender.org/manual/en/dev/render/cycles/nodes/osl.html

#define MAX_TRANSPR 5
#define RAY_MINOFFSET 0.0005

void make_orthonormals(vector N, output vector a, output vector b)
{
	if(N[0] != N[1] || N[0] != N[2]) a = cross(vector(1, 1, 1), N);
	else a = cross(vector(-1, 1, 1), N);

	a = normalize(a);
	b = cross(N, a);
}

point vect_rot(point pnt, float ang, point axis)
{
	if(fabs(ang) > 0.001){
		return rotate(pnt, ang, point(0,0,0), axis);
	}
	return pnt;
}

int do_tracing(point pIni, point pDirection, float distcap, string mask_objname, string skip_objname, output point out_pos) {
	out_pos = pIni;
	int isHit = 0;
	float maxdst = distcap;
	float ismaskset = strlen(mask_objname);
	float isskipset = strlen(skip_objname);
	point pFrom = pIni;
	for(int skipcc = 0;skipcc < MAX_TRANSPR; skipcc++){
		isHit = trace(pFrom+RAY_MINOFFSET*pDirection, pDirection, "maxdist", maxdst);
		if(isHit > 0){
			point tmpP = pFrom;
			getmessage("trace", "P", tmpP);
			//point tmpN = nThis;
			//getmessage("trace", "N", tmpN);
			float tmpHd = 0;
			getmessage("trace", "hitdist", tmpHd);
			if(ismaskset > 0 || isskipset > 0){
				string hitId;
				getmessage("trace", "geom:name", hitId);
				if(ismaskset > 0 && regex_search(hitId, mask_objname) == 0){
					isHit = 0;
				}
				if(isskipset > 0 && regex_search(hitId, skip_objname) > 0){
					isHit = 0;
				}
			}
			if(isHit > 0){
				out_pos = tmpP;
				return 1;
			}
			maxdst = maxdst-tmpHd;
			pFrom = tmpP;
		}
		if(maxdst < RAY_MINOFFSET){
			break;
		}
	}
	return 0;
}

shader simpleRaycast (
	point lt_Dir = point(0, 0, 1),
	float lt_Elevation = 0.01,
	float lt_Escapedst = 0.0,
	point lt_DirRotate = point(0,0,0),
	point opt_TestCone = point(0.0,0.0,0.0),
	string opt_mask_objName = "",
	string opt_skip_objName = "",

	point in_pos = point(0,0,0),
	point in_nrm = point(0,0,0),

	output float out_lvl = 0.0,
	output float out_dst = 0.0
	//output point out_nrm = point(0,0,0),
	//output float out_nvl = 0.0
){
	float coneRad = opt_TestCone[0];
	if(lt_Escapedst < 0.0 || coneRad < 0.0){
		// Fast exit for no-need-to-test cases
		return;
	}
	point pThis = isconnected(in_pos)?in_pos:P;
	point nThis = isconnected(in_nrm)?in_nrm:N;
	point p1 = cross(nThis,point("object", 0, 0, 1));
	point p2 = cross(nThis,p1);
	float lt_Maxxdst = lt_Escapedst>0.0?lt_Escapedst:999.0;
	//out_nrm = nThis;
	//out_pos = pThis;

	point raypos = pThis;
	point lt_DirN;
	lt_DirN = normalize(lt_Dir);
	lt_DirN = vect_rot(lt_DirN, lt_DirRotate[0], p1);
	lt_DirN = vect_rot(lt_DirN, lt_DirRotate[1], p2);
	lt_DirN = vect_rot(lt_DirN, lt_DirRotate[2], nThis);
	int isHit = do_tracing(pThis+lt_DirN*lt_Elevation, lt_DirN, lt_Maxxdst, opt_mask_objName, opt_skip_objName, raypos);
	float originalDot = dot(nThis,lt_DirN);
	if(isHit == 0 && coneRad > 0.0){
		point rp1;
		point rp2;
		make_orthonormals(lt_DirN, rp1, rp2);
		float coneSteps1 = max(1,opt_TestCone[1]);
		float coneSteps2 = max(1,opt_TestCone[2]);
		float csdx = 1/coneSteps1;
		float csdy = 1/coneSteps2;
		for(float ii = -1; ii <= 1 && isHit == 0; ii = ii+csdx){
			for(float jj = -1; jj <= 1 && isHit == 0; jj = jj+csdy){
				point lt_DirN2 = lt_DirN+rp1*ii*coneRad+rp2*jj*coneRad;
				if(originalDot*dot(lt_DirN2,nThis) > 0)
				{
					isHit = do_tracing(pThis+lt_DirN2*lt_Elevation, lt_DirN2, lt_Maxxdst, opt_mask_objName, opt_skip_objName, raypos);
				}
			}
		}
	}
	if(isHit > 0){
		out_lvl = 1;
		out_dst = length(pThis-raypos);
	}
}
